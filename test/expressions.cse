nSubSteps=6

#define PI 3.1415927

wfName = "CA_SACRAMENTO-EXECUTIVE-AP_724830S_STYP20.epw"

doCoverage = Yes

// some data to probe
GAIN GN1 gnPower=$dayOfYear
GAIN GN2 gnPower=$dayOfYear/brkt( 2, $hour, 7)


DELETE REPORT EB

REPORT rpType=UDT rpFreq=Year rpTitle="Some one-off tests"
	REPORTCOL colHead = "FtIn" colVal = 1' 6 == 1.5 ? "Yes" : "No"  colWid=4
	REPORTCOL colHead = "LT" colVal = 4.01+.5 < min( 6, abs( -5)) ? "Yes" : "No"  colWid=4
	REPORTCOL colHead = "LE" colVal = sqrt( 16) <= max( 4.1, -2) ? "Yes" : "No"  colWid=4
	REPORTCOL colHead = "EQ" colVal = sqrt( 16) == ToFloat( 4) ? "Yes" : "No"  colWid=4
	REPORTCOL colHead = "NE" colVal = sqrt( 17) != 4 ? "Yes" : "No"  colWid=4
	REPORTCOL colHead = "GE" colVal = 3 >= abs( -3) ? "Yes" : "No"  colWid=4
	REPORTCOL colHead = "GT" colVal = !(4.01+1 > abs( -5)) ? "No" : "Yes"  colWid=4

ENDREPORT

// TODO: no way to get the error return?
REPORT rpType=UDT rpFreq=Year rpTitle="FileInfo"
	REPORTCOL colHead = "Error" colVal=fileInfo("%") colWid=5
	REPORTCOL colHead = "Empty" colVal = fileInfo("expressions.err") colWid=5
	REPORTCOL colHead = "Norm" colVal = fileInfo(@Top.wfName) colWid=5
	REPORTCOL colHead = "Dir" colVal = fileInfo(".") colWid=5
ENDREPORT

REPORT rpType=UDT rpFreq=Day rpTitle="Dates etc"  rpDayBeg=Apr 20 rpDayEnd=May 10
	REPORTCOL colHead = "Date" colVal = @Top.dateStr
	REPORTCOL colHead = "DOM"  colVal = $dayOfMonth colWid=4
	REPORTCOL colHead = "DOY"  colVal = $dayOfYear colWid=4
	REPORTCOL colHead = "M^Y"  colVal = $dayOfMonth^$dayOfYear colWid=4
	REPORTCOL colHead = "M|Y"  colVal = $dayOfMonth|$dayOfYear colWid=4
	REPORTCOL colHead = "M&Y"  colVal = $dayOfMonth&$dayOfYear colWid=4
	REPORTCOL colHead = "DOW1"  colVal = fix(abs(-($dayOfYear+3)))%7 + 1 colWid=4
	REPORTCOL colHead = "DOW2"  colVal = $dayOfWeek colWid=4
	REPORTCOL colHead = "DOW3"  colVal = $dayOfYear%7 colWid=4
	REPORTCOL colHead = "!DOW3"  colVal = !($dayOfYear%7) colWid=5
	REPORTCOL colHead = "M>W"  colVal = $dayOfMonth>$dayOfWeek colWid=4
	REPORTCOL colHead = "MXMW"  colVal = max( $dayOfMonth, $dayOfWeek, $dayOfYear) colWid=4
	REPORTCOL colHead = "MMMW"  colVal = min( $dayOfMonth, $dayOfWeek, $dayOfYear) colWid=4
	// use select() to force integer logical comparison (avoids float promotion)
	REPORTCOL colHead = "LT" colVal = select( $dayOfWeek < $dayOfMonth, "LT", default "--") colWid=4
	REPORTCOL colHead = "LE" colVal = select( $dayOfWeek <= $dayOfMonth, "LE", default "--") colWid=4
	REPORTCOL colHead = "EQ" colVal = select( $dayOfWeek == $dayOfMonth, "EQ", default "--") colWid=4
	REPORTCOL colHead = "NE" colVal = select( $dayOfWeek != $dayOfMonth, "NE", default "--") colWid=4
	REPORTCOL colHead = "GE" colVal = select( $dayOfWeek >= $dayOfMonth, "GE", default "--") colWid=4
	REPORTCOL colHead = "GT" colVal = select( $dayOfWeek > $dayOfMonth, "GT", default "--") colWid=4
	REPORTCOL colHead = "Sel"  colVal = Select( $dayOfWeek==1, "Sun", $dayOfWeek == 2, "Mon", $dayofWeek == 3, "Tue", $dayOfWeek == 4, "Wed",
	                           $dayOfWeek == 5, "Thu", $dayOfWeek==6, "Fri", $dayofWeek==7, "Sat", default "Er") colWid = 4
	// choose1 w/ default
  	REPORTCOL colHead = "Chs1"  colVal = choose1( $dayofWeek, "Sun", "Mon", "Tue","Wed", "Thu", "Fri", "Sat") colWid = 4
	// choose1 w/o default
  	REPORTCOL colHead = "Chs2"  colVal = choose1( $dayofWeek, "Sun", "Mon", "Tue", default "Oth") colWid = 4
	// shifts
	REPORTCOL colHead = "W>>"  colVal = $dayOfWeek >> 1 colWid=4
	REPORTCOL colHead = "W<<"  colVal = $dayOfWeek << 1 colWid=4
	// not not
	REPORTCOL colHead = "!!"   colVal = !!(($dayOfWeek-1)*$dayOfWeek) colWid=4
	// integer divide
	REPORTCOL colHead = "W/7"   colVal = select( $dayOfWeek/7, "1", default "0") colWid=4
	REPORTCOL colHead = "brkt"  colVal = choose( brkt( 3, $dayOfWeek, 5), "Er0","Sun", "Mon", "Tue","Wed", "Thu", "Fri", "Sat", default "ErX") colWid=4
ENDREPORT



REPORT rpType=UDT rpFreq=Hour rpDayBeg=may 1 rpTitle="log/pow/exp + psychro"
	REPORTCOL colHead = "Hr" colVal=$hour colWid=2
	REPORTCOL colHead = "ExpHr" colVal=exp($hour-12)  colwid=8 colDec=4
	REPORTCOL colHead = "LxHr" colVal=exp(loge($hour))  colwid=6 colDec=2
	REPORTCOL colHead = "LP10Hr" colVal=pow(10., log10($hour))  colwid=6 colDec=2
	REPORTCOL colHead = "tDBO"  colVal=$tDbO colWid=10 colDec=3
	REPORTCOL colHead = "tWbO"  colVal=$tWbO colWid=6 colDec=3
	REPORTCOL colHead = "wO"  colVal=$wO colWid=6 colDec=5
	REPORTCOL colHead = "w1"  colVal=wFromDbWb($tDbO,$tWbO) colWid=6 colDec=5
	REPORTCOL colHead = "rh"  colVal=rhFromDbW($tDbO,$wO) colWid=6 colDec=4
	REPORTCOL colHead = "w2"  colVal=wFromDbRh($tDbO,rhFromDbW($tDbO,$wO)) colWid=6 colDec=4
	REPORTCOL colHead = "enth"  colVal=enthalpy($tDbO,$wO) colWid=6 colDec=2
	REPORTCOL colHead = "brkt"  colVal=brkt( 60, $tdbO, 80.) colWid=6 colDec=2
ENDREPORT


// Trig fuctions.  No check for tan( PI/2) -- HA calc is a little inaccurate, so no overflow
#define HA (($hour-12)*PI/12)
#define HAD (($hour-12)*15)
REPORT rpType=UDT rpFreq=Hour rpDayBeg=jul 1 rpTitle="Trig functions"
	REPORTCOL colHead = "Day" colVal=$dayOfYear colWid=3
	REPORTCOL colHead = "Hr" colVal=$hour colWid=2
	REPORTCOL colHead = "HrAng" colVal = HA colWid=6 colDec=4			// hour angle: radians relative to solar noon (+ afternoon)

	REPORTCOL colHead = "SinHA" colVal = sin( HA) colWid=7 colDec=4
	REPORTCOL colHead = "CosHA" colVal = cos( HA) colWid=7 colDec=4
	REPORTCOL colHead = "C+SHA" colVal = pow( sin( HA),2)+pow( cos( HA),2) colWid=7 colDec=4
	REPORTCOL colHead = "TanHA" colVal = tan( HA) colWid=7 colDec=4
	REPORTCOL colHead = "S/C HA" colVal = sin( HA) / cos( HA) colWid=7 colDec=4
	REPORTCOL colHead = "asinHA" colVal = asin( sin( HA)) colWid=7 colDec=4
	REPORTCOL colHead = "acosHA" colVal = acos( cos( HA)) colWid=7 colDec=4
	REPORTCOL colHead = "atanHA" colVal = atan( tan( HA)) colWid=7 colDec=4
	REPORTCOL colHead = "atan2HA" colVal = atan2( sin( HA),cos( HA)) colWid=7 colDec=4


	REPORTCOL colHead = "HrAngD" colVal = HAD colWid=6 colDec=4			// hour angle: degrees relative to solar noon (+ afternoon)
	REPORTCOL colHead = "SinHAD" colVal = sind( HAD) colWid=7 colDec=4
	REPORTCOL colHead = "CosHAD" colVal = cosd( HAD) colWid=7 colDec=4
	REPORTCOL colHead = "C+SHAD" colVal = pow( sind( HAD),2)+pow( cosd( HAD),2) colWid=7 colDec=4
	REPORTCOL colHead = "TanHAD" colVal = tand( HAD) colWid=7 colDec=4
	REPORTCOL colHead = "S/C HAD" colVal = sind( HAD) / cosd( HAD) colWid=7 colDec=4
	REPORTCOL colHead = "asinHAD" colVal = asind( sind( HAD)) colWid=7 colDec=4
	REPORTCOL colHead = "acosHAD" colVal = acosd( cosd( HAD)) colWid=7 colDec=4
	REPORTCOL colHead = "atanHAD" colVal = atand( tan( HA)) colWid=7 colDec=4
	REPORTCOL colHead = "atan2HAD" colVal = atan2d( sind( HAD),cosd( HAD)) colWid=8 colDec=4
ENDREPORT

REPORT rpType=UDT rpFreq=Hour rpDayBeg=may 1 rpTitle="Probes"
	REPORTCOL colHead = "Hr" colVal=$hour colWid=2
	REPORTCOL colHead = "GN1"  colVal=@GAIN[ "GN1"].gnPower colWid=6 colDec=2
	REPORTCOL colHead = "GN2"  colVal=@GAIN[ 2].gnPower colWid=6 colDec=2
ENDREPORT

RUN

$EOF

