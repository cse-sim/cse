// DHW02.cse -- simple water heating case

// CTZ12 Weather
WfName="CTZ12S13b.CSW"

ebTolHour=.001  ebTolDay=.001 ebTolMon=.001
wuDays=30 nSubSteps=20 

// ACM hourly use schedules
//  should be written to cse file for use in wsUse input
#define HWUSEF_WD hourval( \
 0.014, 0.008, 0.009, 0.011, 0.020, 0.044, 0.089, 0.107, \
 0.089, 0.066, 0.052, 0.038, 0.036, 0.033, 0.032, 0.026, \
 0.042, 0.048, 0.052, 0.047, 0.042, 0.039, 0.036, 0.022 )
#define HWUSEF_WE hourval( \
 0.018, 0.010, 0.009, 0.008, 0.015, 0.023, 0.026, 0.047, \
 0.077, 0.083, 0.074, 0.061, 0.051, 0.043, 0.039, 0.039, \
 0.052, 0.058, 0.056, 0.052, 0.047, 0.044, 0.040, 0.028)

// single family demand-controlled recirc pumping power by hour (Wh)
// App B, Table B-8
#define SF_RECIRCDC hourval( \
 1.0, 0.5, 0.6, 0.6, 1.2, 2.4, 4.5, 5.7, \
 5.4, 4.5, 3.7, 2.8, 2.5, 2.3, 2.1, 1.9, \
 2.8, 3.2, 3.3, 3.1, 2.7, 2.5, 2.3, 1.5 )


 // daily use for 1 unit, gpd, App B Eqn 8
 #define HWUSE1(cfa) (21.4+0.00679*min( cfa, 2500))

 // SDLM
 #define HWSDLM(cfa) (1.004+0.000202*cfa-0.0000000231*cfa*cfa)

METER Elec0
METER Fuel0

// single unit with LDEF calc
DHWSYS "DS0"
  wsElecMtr = Elec0
  wsFuelMtr = Fuel0
  wsTUse = 123
  wsUse = HWUSE1( 1900) * select( $isWeekend, HWUSEF_WE, default HWUSEF_WD)
  wsDSM = 1.4
  wsSDLM = HWSDLM( 1900)
  wsParElec = fix($hour+12)%12
  wsCalcMode = PRERUN

DHWHEATER "WH0"
  whHeatSrc = Fuel
    whMult = 2
    whType = SmallStorage
	whParElec = $month
	whEF = 0.83

DELETE REPORT "EB"

RUN			// pre-run

METER Elec1
METER Fuel1

#define NUnitB 1	// units per branch
#define NUnit 20	// total units
#define CFAi 1200	// CFA of ith unit
#define HWWF .9		// waste factor (.9 for recirc)

 
DHWSYS "DS1"
  wsElecMtr = Elec1
  wsFuelMtr = Fuel1
  wsTUse = 130
  wsUse = NUnit * HWWF * HWUSE1( CFAi) * select( $isWeekend, HWUSEF_WE, default HWUSEF_WD)
  wsDSM = 1.6
  wsSDLM = HWSDLM( CFAi)
  wsSSF = 0.75
  wsParElec = SF_RECIRCDC

  DHWHEATER "WH1A"  whHeatSrc = Fuel  whType = SmallInstantaneous whEF = .65

  DHWHEATER "WH1B" whHeatSrc = Fuel  whType = SmallStorage whLDEF = .9

  DHWHEATER "WH1C"  whHeatSrc = Fuel  whType = LargeInstantaneous whEFF = .80 whPilotPwr = 750

  DHWHEATER "WH1D"  whHeatSrc = Fuel   whType = LargeStorage whEFF = .80 whSBL = 1200

  DHWHEATER "WH1E"  whHeatSrc = Resistance  whType = SmallInstantaneous whEF = .65

  DHWHEATER "WH1F" whHeatSrc = Resistance  whType = SmallStorage whLDEF = .83

  DHWHEATER "WH1G"  whHeatSrc = Resistance  whType = LargeInstantaneous whEFF = .80 whParElec = 3
  
  DHWHEATER "WH1H"  whHeatSrc = Resistance  whType = LargeStorage whEFF = .80 whVol = 100 whSBL = 0.6768239*3413
  
  DHWHEATER "WH1I" whHeatSrc = ASHP whType = SmallStorage	whLDEF = 3.3909

  DHWTANK "Tank1" wtMult = 2  wtVol = 100 wtXLoss = 61.4 wtTTank = 124 wtInsulR = 12 wtTEx = 75

  DHWLOOP "Loop1" wlMult = 1 wlRunF = .2 wlFUA = 2

	DHWLOOPSEG "Seg1"  wgTy=Supply wgLength = 18 wgSize = 2 wgInsulK = 0.26/12 wgInsulThk = 1.5 wgfNoDraw =.2

	   DHWLOOPBRANCH "Br1" wbMult = 20 wbLength = 14 wbSize = 1 wbInsulK = 0.26/12 wbInsulThk = 1.5 wbFlow=2
	    wbfWaste = 1.4 * 19.84 * exp( -0.544 * NUnitB) * hourval( 0.01, 0.02, 0.05, 0.22, 0.25, 0.22, 0.06, 0.01, 0.01, 0.01, 0.01, 0.01, 0.01, 0.01, 0.01, 0.01, 0.01, 0.01, 0.01, 0.01, 0.01, 0.01, 0.01, 0.01)

	DHWLOOPSEG "Seg2"  wgTy=Supply wgLength = 86.6025 wgSize = 2 wgInsulK = 0.26/12 wgInsulThk = 1.5 wgfNoDraw =.4

	DHWLOOPSEG "Seg3"  wgTy=Supply wgLength = 86.6025 wgSize = 2 wgInsulK = 0.26/12 wgInsulThk = 1.5 wgfNoDraw =.6

	DHWLOOPSEG "Seg4"  wgTy=Return wgLength = 86.6025 wgSize = .75 wgInsulK = 0.26/12 wgInsulThk = 1.5 wgfNoDraw = 1

	DHWLOOPSEG "Seg5"  wgTy=Return wgLength = 86.6025 wgSize = .75 wgInsulK = 0.26/12 wgInsulThk = 1.5 wgfNoDraw = 1

	DHWLOOPSEG "Seg6"  wgTy=Return wgLength = 18 wgSize = .75 wgInsulK = 0.26/12 wgInsulThk = 1.5 wgfNoDraw = 1

	DHWLOOPPUMP "LP1"  wlpPwr = 746 * 0.5 / 0.9  wlpLiqHeatF = 0.6


endDHWSYS

// DHW report definitions

// DHWSYS
#define WSREP( s, d) REPORT rpType=UDT rpFreq=HOUR rpTitle = s rpDayBeg=d rpDayEnd=d \
ReportCol colhead="Hr" colVal=$hour colWid = 2 \
ReportCol colhead="Tinlet" colVal=@DhwSys[s].tInlet colWid=6 \
ReportCol colhead="HWUse"  colVal=@DhwSys[s].hwUse colDec=2 colWid=6 \
ReportCol colhead="HHWO" colVal=@DhwSys[s].HHWO colWid = 7 \
ReportCol colhead="DLM" colVal=@DhwSys[s].DLM colWid = 7 \
ReportCol colhead="SSF" colVal=@DhwSys[s].SSF colDec=3 colWid = 7 \
ReportCol colhead="HRDL" colVal=@DhwSys[s].HRDL colWid = 7 \
ReportCol colhead="HJL" colVal=@DhwSys[s].HJL colWid = 7 \
ReportCol colhead="HARL" colVal=@DhwSys[s].HARL colWid = 7 \
ReportCol colhead="Elec" colVal=@DhwSys[s].inElec colDec=2 colWid = 6

#define CX(s,IVL,c) (@DHWSYSRES[ s].IVL.c/1000)
#define CXBal( s, IVL) CX(s,IVL,qLoad)+CX(s,IVL,qLoss)+CX(s,IVL,qLoop)-CX(s,IVL,qDWHR)-CX(s,IVL,qSSF)-CX(s,IVL,qSolar)-CX(s,IVL,qWH)-CX(s,IVL,qLH)-CX(s,IVL,qXBU)
#define WSRREPCOLS( s, IVL) \
ReportCol colhead = "Load" colVal=CX(s,IVL,qLoad) colWid=8 \
ReportCol colhead = "Loss" colVal=CX(s,IVL,qLoss) colWid=8 \
ReportCol colhead = "Loop" colVal=CX(s,IVL,qLoop) colWid=8 \
ReportCol colhead = "DWHR" colVal=CX(s,IVL,qDWHR) colWid=8 \
ReportCol colhead = "SSF" colVal=CX(s,IVL,qSSF) colWid=8 \
ReportCol colhead = "Solar" colVal=CX(s,IVL,qSolar) colWid=8 \
ReportCol colhead = "WH" colVal=CX(s,IVL,qWH) colWid=8 \
ReportCol colhead = "LH" colVal=CX(s,IVL,qLH) colWid=8 \
ReportCol colhead = "XBU" colVal=CX(s,IVL,qXBU) colWid=8 \
ReportCol colhead = "Bal" colVal = CXBal(s,IVL) colWid=8

#define WSRREP_H( s, dBeg, dEnd) REPORT rpType=UDT rpFreq=HOUR rpTitle = s rpDayBeg=dBeg rpDayEnd=dEnd rpCPL=-1 \
ReportCol colhead="Hr" colVal=$hour colWid = 3 \
WSRREPCOLS( s, H)

#define WSRREP_D( s, dBeg, dEnd) REPORT rpType=UDT rpFreq=DAY rpTitle = s rpDayBeg=dBeg rpDayEnd=dEnd rpCPL=-1 \
ReportCol colhead="Day" colVal=@Top.dateStr colWid = 10 \
WSRREPCOLS( s, D)

#define WSRREP_M( s) REPORT rpType=UDT rpFreq=MONTH rpTitle = s rpDayBeg=jan 1 rpDayEnd=dec 31 rpCPL=-1 \
ReportCol colhead="Mon" colVal=@Top.monStr colWid = 3 \
WSRREPCOLS( s, M) \
REPORT rpType=UDT rpFreq=YEAR rpHeader=No rpCPL=-1 \
ReportCol colVal="Yr" colWid = 3 \
WSRREPCOLS( s, Y)


// DHWHEATER
#define WHREP( h, d) REPORT rpType=UDT rpFreq=HOUR rpTitle = @DhwHeater[h].desc rpDayBeg=d rpDayEnd=d \
ReportCol colhead="Hr" colVal=$hour colWid = 2 \
ReportCol colhead="LDEF" colVal=@DhwHeater[h].LDEF colDec=2 colWid = 6 \
ReportCol colhead="tIn" colVal=@DhwHeater[h].tInlet colDec=2 colWid = 6 \
ReportCol colhead="Elec" colVal=@DhwHeater[h].inElec colDec=2 colWid = 6 \
ReportCol colhead="Fuel" colVal=@DhwHeater[h].inFuel colDec=2 colWid = 6

// DHWTANK
#define WTREP( t, d) REPORT rpType=UDT rpFreq=HOUR rpTitle = t rpDayBeg=d rpDayEnd=d \
ReportCol colhead="Hr" colVal=$hour colWid = 2 \
ReportCol colhead="UA" colVal=@DhwTank[t].UA colDec=3 colWid = 6 \
ReportCol colhead="Ttank" colVal=@DhwTank[t].ttank colDec=2 colWid=6 \
ReportCol colhead="TEx" colVal=@DhwTank[t].tEx colDec=2 colWid=6 \
ReportCol colhead="XLoss" colVal=@DhwTank[t].xloss colDec=2 colWid=6 \
ReportCol colhead="QLoss" colVal=@DhwTank[t].qloss colDec=2 colWid=6


// DHWLOOP
#define WLREP( l, d) REPORT rpType=UDT rpFreq=HOUR rpTitle = l rpDayBeg=d rpDayEnd=d \
ReportCol colhead="Hr" colVal=$hour colWid = 2 \
ReportCol colhead="HRLL" colVal=@DhwLoop[l].HRLL colWid = 7 \
ReportCol colhead="HRBL" colVal=@DhwLoop[l].HRBL colWid = 7

// DHWLOOPSEG
#define WGREP( g, d) REPORT rpType=UDT rpFreq=HOUR rpTitle = g rpDayBeg=d rpDayEnd=d \
ReportCol colhead="Hr" colVal=$hour colWid = 2 \
ReportCol colhead="UA" colVal=@DhwLoopSeg[g].totals.UA colDec=3 colWid = 6 \
ReportCol colhead="vol" colVal=@DhwLoopSeg[g].totals.vol colDec=3 colWid = 6 \
ReportCol colhead="Flow" colVal=@DhwLoopSeg[g].fvf colDec=2 colWid = 6 \
ReportCol colhead="Tex" colVal=@DhwLoopSeg[g].ExT colDec=2 colWid = 6 \
ReportCol colhead="Tin" colVal=@DhwLoopSeg[g].tIn colDec=3 colWid = 7 \
ReportCol colhead="Tout" colVal=@DhwLoopSeg[g].tOut colDec=3 colWid = 7 \
ReportCol colhead="PLWF" colVal=@DhwLoopSeg[g].PLWF colWid = 7 \
ReportCol colhead="PLCD" colVal=@DhwLoopSeg[g].PLCD colWid = 7

// DHWLOOPBRANCH
#define WBREP( b, d) REPORT rpType=UDT rpFreq=HOUR rpTitle = b rpDayBeg=d rpDayEnd=d \
ReportCol colhead="Hr" colVal=$hour colWid = 2 \
ReportCol colhead="UA" colVal=@DhwLoopBranch[b].totals.UA colDec=3 colWid = 6 \
ReportCol colhead="vol" colVal=@DhwLoopBranch[b].totals.vol colDec=3 colWid = 6 \
ReportCol colhead="Flow" colVal=@DhwLoopBranch[b].fvf colDec=2 colWid = 6 \
ReportCol colhead="Tex" colVal=@DhwLoopBranch[b].ExT colDec=2 colWid = 6 \
ReportCol colhead="Tin" colVal=@DhwLoopBranch[b].tIn colDec=2 colWid = 6 \
ReportCol colhead="Tout" colVal=@DhwLoopBranch[b].tOut colDec=2 colWid = 6 \
ReportCol colhead="HBUL" colVal=@DhwLoopBranch[b].HBUL colWid = 7 \
ReportCol colhead="HBWL" colVal=@DhwLoopBranch[b].HBWL colWid = 7

// DHWLOOPPUMP
#define WLPREP( p, d) REPORT rpType=UDT rpFreq=HOUR rpTitle = p rpDayBeg=d rpDayEnd=d \
ReportCol colhead="Hr" colVal=$hour colWid = 2 \
ReportCol colhead="Pwr" colVal=@DhwLoopPump[p].pwr colDec=1 colWid = 5 \
ReportCol colhead="Elec" colVal=@DhwLoopPump[p].inElec colDec=2 colWid = 6


// Weather
#define WTHRREP( d) REPORT rpType=UDT rpFreq=Day rpTitle = "Weather" rpDayBeg=d rpDayEnd=d \
ReportCol colhead="Day" colVal=@Top.dateStr colWid = 12 \
ReportCol colhead="DOY" colVal=$dayOfYear colWid = 3 \
ReportCol colhead="Tavg31" colVal=@weather.taDbAvg31 colDec = 3 colWid = 6 \
ReportCol colhead="tMains" colVal=@weather.tMains  colDec=3  colWid=6


WTHRREP( apr 1)

REPORT rpType=MTR rpMeter = Elec0 rpFreq=MONTH
REPORT rpType=MTR rpMeter = Fuel0 rpFreq=MONTH

REPORT rpType=MTR rpMeter = Elec0 rpFreq=HOUR rpDayBeg=apr 1  rpDayEnd=apr 1  rpBtuSf = 1
REPORT rpType=MTR rpMeter = Fuel0 rpFreq=HOUR rpDayBeg=apr 1  rpDayEnd=apr 1  rpBtuSf = 1

WSRREP_M( "DS0")

WSREP( "DS0", apr 1)
WSRREP_H( "DS0", apr 1, apr 1)

WHREP( "WH0", apr 1)

REPORT rpType=MTR rpMeter = Elec1 rpFreq=MONTH
REPORT rpType=MTR rpMeter = Fuel1 rpFreq=MONTH

REPORT rpType=MTR rpMeter = Elec1 rpFreq=HOUR rpDayBeg=apr 1  rpDayEnd=apr 1  rpBtuSf = 1
REPORT rpType=MTR rpMeter = Fuel1 rpFreq=HOUR rpDayBeg=apr 1  rpDayEnd=apr 1  rpBtuSf = 1

WSRREP_M( "DS1")

WSREP( "DS1", apr 1)
WSRREP_H( "DS0", apr 1, apr 1)

WHREP( "WH1A", apr 1)
WHREP( "WH1B", apr 1)
WHREP( "WH1C", apr 1)
WHREP( "WH1D", apr 1)
WHREP( "WH1E", apr 1)
WHREP( "WH1F", apr 1)
WHREP( "WH1G", apr 1)
WHREP( "WH1H", apr 1)
WHREP( "WH1I", apr 1)



WTREP( "Tank1", apr 1)

WLREP( "Loop1", apr 1)
WLPREP( "LP1", apr 1)

WGREP( "Seg1", apr 1)
WGREP( "Seg2", apr 1)
WGREP( "Seg3", apr 1)
WGREP( "Seg4", apr 1)
WGREP( "Seg5", apr 1)
WGREP( "Seg6", apr 1)
WBREP( "Br1", apr 1)


 
RUN





